set nocompatible  " Must be first: use Vim defaults (much better!)

" Plugins may use the mapleader, so set first.
let mapleader = ","

" Should occaianly call `:PlugUpdate` from vim.
call plug#begin('~/.vim/plugged')
Plug 'altercation/vim-colors-solarized'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-speeddating'
autocmd VimEnter * if exists(":SpeedDatingFormat") |
    \ SpeedDatingFormat %d%b%y | endif  " sdate
Plug 'jpalardy/vim-slime'
if v:version > 702
    Plug 'myusuf3/numbers.vim'
endif
set number " Line numbers, need even if `myusuf3/numbers.vim` isn't there.
Plug 'rstacruz/sparkup'
Plug 'gregsexton/MatchTag'
Plug 'mhinz/vim-signify'
Plug 'tmhedberg/matchit'
Plug 'bling/vim-airline'
let g:airline_powerline_fonts = 1
let g:airline_left_sep='›' " Slightly fancier than '>'
let g:airline_right_sep='‹' " Slightly fancier than '<'
set laststatus=2  " Always show.
Plug 'bling/vim-bufferline'
if v:version < 704
    nnoremap / /\v
    vnoremap / /\v
else
    Plug 'haya14busa/incsearch.vim'
    autocmd VimEnter * if exists(":IncSearchMap") |
        \ map / <Plug>(incsearch-forward)|
        \ map ? <Plug>(incsearch-backward)|
        \ map g/ <Plug>(incsearch-stay)|
        \ let g:incsearch#auto_nohlsearch = 1 |
        \ let g:incsearch#magic = '\v' |
        \ endif
endif
" IncSearch doesn't do this, and can't be on the same line.
cnoremap %s/ %s/\v
set hlsearch
set wrapscan  " Always wrap around.
set ignorecase  " This makes search case insensitive when the pattern is all lowercase.
set smartcase
set gdefault  " All substitutions are global
call plug#end()

filetype plugin indent on

" When editing a file, always jump to the last cursor position
autocmd BufReadPost * if line("'\"") | exe "'\"" | endif

set bs=2  " allow backspacing over everything in insert mode
set nobackup  " no   keep a backup file
set swapfile  " If opening a large file, this is tunred off in `v`.
set viminfo='20,\"50  " .viminfo, don't store more than 50 lines of registers
set history=50  " keep 50 lines of command line history
set tabstop=4  " set the tab stop, duh!
set shiftwidth=4  " the autoindent stuff
set softtabstop=4  " Also sts
set expandtab  " So we get spaces.
set shiftround  " Use muliple of shiftwith with <>
set nofoldenable  " Start w/o folds, enable when a fold cmd is used.
set pastetoggle=<F11>
set scrolloff=5  "Keep five lines showeng around cursor.
" Wrap long lines in the buffer, but wrap on-screen at word boundries.
set textwidth=0
set wrapmargin=0
set lbr
set wrap
set showmatch  " Briefly jump to the matching paren when one is inserted.
set showmode
set encoding=utf-8
set showcmd
set wildmenu
set wildmode=list:longest
set visualbell
set cursorline
set ttyfast
set backspace=indent,eol,start
set nrformats-=octal  " Don't treat 0 prefixed numbers as octal with ^A
set ttimeout
set ttimeoutlen=100
set display+=lastline
if &listchars ==# 'eol:$'
    set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+
endif
if v:version > 703 || v:version == 703 && has("patch541")
    set formatoptions+=j  " Del comment char when joining lines.
endif
set autoread
set fileformats+=mac
set modelines=0  " Do not use modelines, from other people.
set hidden  "Save the buffer, like when useing :e
set ai  " always set autoindenting on

" Visual shifting (does not exit Visual mode), donno about this.
vnoremap < <gv
vnoremap > >gv

" Sometimes detected as Modula-2, which I'll never use.
autocmd BufNewFile,BufReadPost *.md set filetype=markdown
" Switch syntax highlighting on, when the terminal has colors
syntax enable
set t_Co=256
set background=dark
try
    colorscheme solarized
catch /^Vim\%((\a\+)\)\=:E185/
    " Do nothing.
endtry
if &term=="xterm"
     set t_Co=8
     set t_Sb=^[4%dm
     set t_Sf=^[3%dm
endif
" After the color scheme.
if v:version > 702
    set colorcolumn=80
else
    highlight OverLength ctermbg=red ctermfg=white guibg=#592929
    match OverLength /\%81v.\+/
endif

set cindent

augroup cprog
    " Remove all cprog autocommands
    au!

    " When starting to edit a file:
    "   For C and C++ files set formatting of comments and set C-indenting on.
    "   For other files switch it off.
    "   Don't change the order, it's important that the line with * comes first.
    autocmd FileType *      set formatoptions=tcql nocindent comments&
    autocmd FileType c,cpp  set formatoptions=croql cindent comments=sr:/*,mb:*,el:*/,://
augroup END

" Don't use Ex mode, use Q for formatting
map Q gq

" OK, this is weird, use ; as well as : for the same thing.
nnoremap ; :
vnoremap ; :
" What to do with ; ? (repeart the last f{char} or t.
" Same with ,
" Set at top: let mapleader = ","
" and now \ is what ; used to be (re-do last f, F, t, or T).
nnoremap \ ;
vnoremap \ ;


execute 'nnoremap <leader>a :w!<CR>:!aspell --encoding=UTF-8 --mode=email check "%"<CR>:e! "%"<CR><CR>'
execute 'nnoremap <leader>ah :w!<CR>:!aspell --encoding=UTF-8 --mode=html check "%"<CR>:e! "%"<CR><CR>'
execute 'nnoremap <leader>at :w!<CR>:!aspell --encoding=UTF-8 --mode=tex check "%"<CR>:e! "%"<CR><CR>'
" This is sdate.
execute 'nnoremap <leader>s :r!sdate<CR>kJ$'
" This is the total thing
execute 'nnoremap <leader>t yypkI.005+(<Esc>A)<Esc>!!b<CR>0f.lllDA<Space>'
" BC !
execute 'nnoremap <leader>b !!b<CR>'
" URL convert
execute 'nnoremap <leader>u !!tr-url-escapes<CR>'
" run addr
execute 'nnoremap <leader>d !!addr<CR>'

" This is totally awesome - remap jj to escape in insert mode.
" You'll never type jj anyway, so it's great!
inoremap jj <Esc>
